ENTRY(nautilus_entry)

SECTIONS
{
    . = 0x100000;

    .boot :
    {
        *(.mbhdr)
        _loadStart = .;
        *(.boot)
        . = ALIGN(4096);
        pml4 = .;
        . += 0x1000;
        pdpt = .;
        . += 0x1000;
        pd = .;
        . += 0x4000; /* room to map 4 GB at PD level */

        boot_stack_start = .;
        . += 0x200000;  /* we start out with a 2M stack */
        boot_stack_end = .;
    }


    .text ALIGN(0x1000) : 
    {
        *(.text*)
        *(.gnu.linkonce.t*)
    }

    .init ALIGN(0x1000) : AT(ADDR(.text) + SIZEOF(.text))
    {
        *(.init)
        *(.gnu.linkonce.init)
    }

    .fini ALIGN(0x1000) : AT(ADDR(.init) + SIZEOF(.init))
    {
        *(.fini)
        *(.gnu.linkonce.fini)
    }

    .init_array ALIGN(0x1000) : AT(ADDR(.fini) + SIZEOF(.fini))
    {
        _init_array_start = .;
        *(.init_array*)
        _init_array_end = .;
        *(.gnu.linkonce.init_array*)

    }


    .gcc_except_table ALIGN(0x1000) : AT(ADDR(.init_array) + SIZEOF(.init_array))
    {
        *(.gcc_except_table*)
        *(.gnu.linkonce.gcc_except*)
    }

    _data_start = . ;    

    .data ALIGN(0x1000) : AT(ADDR(.gcc_except_table) + SIZEOF(.gcc_except_table))
    {
        *(.data*)
        *(.gnu.linkonce.d*)
    }

    .tests ALIGN(0x1000) : AT(ADDR(.data) + SIZEOF(.data))
    {
        __start_tests = .;
        *(.tests*);
        __stop_tests = .;
    }

    .cmdline_flags ALIGN(0x1000) : AT(ADDR(.tests) + SIZEOF(.tests))
    {
        __start_flags = .;
        *(.cmdline_flags*);
        __stop_flags = .;
    }
   
    .got ALIGN(0x1000) : AT(ADDR(.cmdline_flags)+SIZEOF(.cmdline_flags))
    {
        *(.got*)
        *(.gnu.linkconce.got*)
    }

#include "rodata_common.ld.inc"

   .tdata  ALIGN(0x1000) : AT(ADDR(.alloc_impls)+SIZEOF(.alloc_impls))
   {
       _tdata_start = .;

       *(.tdata .tdata*);
       . = ALIGN(0x1000);
       _tdata_end = .;
   }

  
   _loadEnd = .;
   .tbss : AT(ADDR(.tdata)+SIZEOF(.tdata))
   {
       _tbss_start = .;
       *(.tbss .tbss.*) *(.tcommon) ;
       . = ALIGN(0x1000);
       . += 0x1000;
       _tbss_end = .;
   }
  
   _bssStart = .;
   .bss ALIGN(0x1000) : AT(ADDR(.tbss)+SIZEOF(.tbss))
    {
        *(COMMON)
        *(.bss*)
        *(.gnu.linkonce.b*)
    }
    
    _bssEnd = .;
   
    _data_end = .;
   
    
    /DISCARD/ :
    {
        *(.note*) /* qemu -kernel doesn't work without the PVH note section */
        *(.comment)
        *(.eh_frame)
    }

    PROVIDE(kernel_end = .);
}

