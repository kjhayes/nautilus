OUTPUT_ARCH( "riscv" )
ENTRY( nautilus_entry )

SECTIONS
{
  /*
   * ensure that entry.S / _entry is at 0x80000000,
   * where qemu's -kernel jumps.
   */
  . = NAUT_CONFIG_KERNEL_LINK_ADDR;

  PROVIDE(kernel_start = .);

  .text ALIGN(0x1000) : {
    _loadStart = .;
    *(.boot)
    . = ALIGN(0x1000);
    pml4 = .;
    . += 0x1000;

    boot_stack_start = .;
    . += 0x200000;  /* we start out with a 2M stack */
    boot_stack_end = .;
    *(.text* .text.*)
    . = ALIGN(0x1000);
    etext = .;
  }

  _dataStart = .;

  .data : {
    . = ALIGN(16);
    *(.sdata .sdata.*) /* do not need to distinguish this from .data */
    . = ALIGN(16);
    *(.data .data.*)
  }

/* Needs to be RW */
#include "init_stages.ld.inc"

#include "rodata_common.ld.inc"

  _loadEnd = .;

  _bssStart = .;
  .bss ALIGN(0x1000) : {
    . = ALIGN(16);
    *(.sbss .sbss.*) /* do not need to distinguish this from .bss */
    . = ALIGN(16);
    *(.bss .bss.*)
  }

  _bssEnd = .;

  _dataEnd = .;

  PROVIDE(kernel_end = .);
}
