OUTPUT_ARCH( "aarch64" )
ENTRY( nautilus_entry )

SECTIONS
{
  . = NAUT_CONFIG_KERNEL_LINK_ADDR;

  PROVIDE(kernel_start = .);

  .text ALIGN(0x1000) : {
    _loadStart = .;
    *(.boot)
    . = ALIGN(0x1000);
    pml4 = .;
    . += 0x1000; /* Probably no need for the space but the symbol pml4 is needed for now */
    . = ALIGN(0x1000);
    _textStart = .;
    *(.text* .text.*)
    . = ALIGN(0x1000);
    _textEnd = .;
    boot_stack_start = .;
    . += 0x1000;  /* Allocate space for the boot stack */
    boot_stack_end = .;
    etext = .;
    . = ALIGN(0x1000);
  }

  .data ALIGN(0x1000) : {
    *(.sdata .sdata.*) /* do not need to distinguish this from .data */
    . = ALIGN(16);
    *(.data .data.*)
  }


  _rodataStart = .;
  .rodata ALIGN(0x1000) : {
    *(.srodata .srodata.*) /* do not need to distinguish this from .rodata */
    . = ALIGN(0x1000);
    *(.rodata .rodata.*)
  }

  .cmdline_flags ALIGN(0x1000) : {
    __start_flags = .;
    *(.cmdline_flags*);
    __stop_flags = .;
  }

  .shell_cmds ALIGN(0x1000) : {
    __start_shell_cmds = .;
    KEEP(*(SORT_BY_NAME(.shell_cmd.*)));
    __stop_shell_cmds = .;
  }
  _rodataEnd = .;
  _loadEnd = .;

  .bss ALIGN(0x1000) : {
    . = ALIGN(16);
    _bssStart = .;
    *(.sbss .sbss.*) /* do not need to distinguish this from .bss */
    . = ALIGN(16);
    *(.bss .bss.*)
    _bssEnd = .;
  }


  PROVIDE(kernel_end = .);
}
